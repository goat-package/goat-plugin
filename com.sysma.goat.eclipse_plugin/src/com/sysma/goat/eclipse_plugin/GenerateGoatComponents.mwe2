module com.sysma.goat.eclipse_plugin.GenerateGoatComponents

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "com.sysma.goat.eclipse_plugin"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "com.sysma.goat.eclipse_plugin.GoatInfrastructure"
			fileExtensions = "ginf"
			
			generator = {
				generateXtendMain = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
				//composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				//composedCheck = "org.sysma.goat.eclipse_plugin.validator.GoatInfrastructureValidator"
			}
			
			parserGenerator = {
				combinedGrammar = false
			}
		}
		language = StandardLanguage {
			name = "com.sysma.goat.eclipse_plugin.GoatComponents"
			fileExtensions = "goat"
			referencedResource = "platform:/resource/com.sysma.goat.eclipse_plugin/model/generated/GoatInfrastructure.genmodel"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				composedCheck = "com.sysma.goat.eclipse_plugin.validation.FunctionValidator"
				composedCheck = "com.sysma.goat.eclipse_plugin.validation.ExpressionContextValidator"
				composedCheck = "com.sysma.goat.eclipse_plugin.validation.ExpressionTypingValidator"
			}
			
			newProjectWizardForEclipse = {
				pluginProject = false
				generate = true
			}
			
			parserGenerator = {
				combinedGrammar = false
			}
		}
	}
}
